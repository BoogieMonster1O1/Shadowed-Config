buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.+'
    }
}

plugins {
    id 'maven-publish'
    id 'fabric-loom' version "0.4-SNAPSHOT"
    id 'com.github.johnrengelman.shadow' version '6.0.0'
}

apply plugin: 'com.jfrog.bintray'

allprojects {
    apply plugin: 'java'
    apply plugin: 'fabric-loom'

    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
    version = project.mod_version
    group = project.maven_group

    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = 'Fabric'
            url = 'http://maven.fabricmc.net/'
        }
        maven {
            name = 'SpongePowered'
            url = 'http://repo.spongepowered.org/maven'
        }
        maven {
            name = 'Mojang'
            url = 'https://libraries.minecraft.net/'
        }
        maven {
            name = 'legacy-fabric'
            url = 'https://dl.bintray.com/legacy-fabric/Legacy-Fabric-Maven'
        }
        maven {
            name = 'Jitpack'
            url = 'https://jitpack.io'
        }
    }

    minecraft {
        intermediaryUrl = {
            return "https://dl.bintray.com/legacy-fabric/Legacy-Fabric-Maven/net/fabricmc/intermediary/" + it + "/intermediary-" + it + "-v2.jar";
        }
    }

    dependencies {
        implementation "com.google.guava:guava:23.5-jre"
        minecraft "com.mojang:minecraft:${project.minecraft_version}"
        mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
        modImplementation ("net.fabricmc:fabric-loader-1.8.9:${project.loader_version}") {
            exclude module: "guava"
        }
        modImplementation "net.fabricmc.fabric-api:fabric-api:0.9.1"
        modImplementation("com.mojang:datafixerupper:4.0.26") {
            exclude group: 'com.google.code.findbugs'
            exclude group: 'com.google.code.gson'
            exclude group: 'com.google.guava'
            exclude group: 'org.apache.commons'
        }
        implementation "blue.endless:jankson:1.1.0"
    }
}

archivesBaseName = project.archives_base_name

configurations {
    shade
}

tasks.remapJar.dependsOn(shadowJar)
(tasks.remapJar.input as FileSystemLocationProperty<? extends FileSystemLocation>).set(shadowJar.archivePath)

dependencies {
    shade "com.mojang:datafixerupper:4.0.26"
    shade "blue.endless:jankson:1.1.0"
    shade 'it.unimi.dsi:fastutil:7.1.0'
    subprojects.each {
        implementation ":${it.path}"
        shade ":${it.path}"
    }
}

processResources {
    inputs.property "version", project.version

    from(sourceSets.main.resources.srcDirs) {
        include "fabric.mod.json"
        expand "version": project.version
    }

    from(sourceSets.main.resources.srcDirs) {
        exclude "fabric.mod.json"
    }
}

tasks.withType(JavaCompile) {
    options.encoding = "UTF-8"
}

// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
// if it is present.
// If you remove this task, sources will not be generated.
task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

jar {
    from "LICENSE"
}

// configure the maven publication
publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact(remapJar) {
                builtBy remapJar
            }
            artifact(sourcesJar) {
                builtBy remapSourcesJar
            }
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'Library that includes DFU and Jankson')
                root.appendNode('name', 'Shadowed-Config')
                root.appendNode('url', 'https://github.com/Legacy-Fabric-Community/shadowed-config')
            }
        }
    }

    // select the repositories you want to publish to
    repositories {
        // uncomment to publish to the local maven
        // mavenLocal()
    }
}

bintray {
    user = project.hasProperty('bintrayUser') ? project.property('bintrayUser') : System.getenv('BINTRAY_USER')
    key = project.hasProperty('bintrayApiKey') ? project.property('bintrayApiKey') : System.getenv('BINTRAY_KEY')
    publications = ["mavenJava"]
    publish = true
    pkg {
        repo = "cool-mods"
        name = "Shadowed-Config"
        licenses = ["CC0-1.0"]
        version {
            released = new Date()
            githubRepo = 'Legacy-Fabric-Community/shadowed-config'
            websiteUrl = 'https://github.com/Legacy-Fabric-Community/shadowed-config'
            issueTrackerUrl = 'https://github.com/Legacy-Fabric-Community/shadowed-config/issues'
            vcsUrl = 'https://github.com/Legacy-Fabric-Community/shadowed-config.git'
            gpg {
                sign = true
            }
        }
    }
}

shadowJar {
    dependencies {
        exclude(dependency('com.mojang:minecraft:.*'))
        exclude(dependency('net.fabricmc:.*:.*'))
        exclude(dependency('com.google.code.findbugs:.*:.*'))
        exclude(dependency('com.google.code.gson:.*:.*'))
        exclude(dependency('com.google.guava:.*:.*'))
        exclude(dependency('org.apache.commons:.*:.*'))
        exclude(dependency('org.apache.logging.log4j:.*:.*'))
    }
    configurations = [project.configurations.shade]
}
